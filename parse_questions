# to run: $ awk -f parse_questions ./forum_data/forum_node.tsv > ./forum_data/parsed_questions.csv
# $1 id, $2 title, $5 body, $6 type, $7 parent id $10 score

BEGIN { FS = "\t" }

{ gsub(/ *<[^>]*> */, " ") }
{ gsub(/[\\\/%]/, " ") }
{ gsub(/"/, "") }

$6 ~ /question/ { questions[$1] = $2 " " $5 }

$6 ~ /answer/ {
  if (answers[$7]) {
    answers[$7] = answers[$7] ",\"" $1 "\",\"" $10 "\""
  } else {
    answers[$7] = "\"" $1 "\",\"" $10 "\""
  }
}

END {
  for (id in questions) {
    if (answers[id]) {
      print "\"" questions[id] "\"," answers[id]
    }
  }
}
